%import common.WS
%ignore WS
%ignore COMMENT

COMMENT: /#(?!(include)).*\r?\n/
QUOTED_STRING: /"[^"]*"/
MAGIC_PATH: /<[^" >][^>]*>/
VARIABLE: /@\{[a-zA-Z][a-zA-Z0-9_]*\}/
UNQUOTED_FILEGLOB: /\/[^ \t\n\r\f\v,]+/
UNQUOTED_VALUE: /[^ \t\n\r\f\v,]+/
UNQUOTED_PROFILE_NAME: /[a-zA-Z0-9][^ \t\n\r\f\v]*/
CAPABILITY: /[a-z][a-z_]+/
PROFILE_FLAG: "complain" | "audit" | "enforce" | "mediate_deleted" | "attach_disconnected" | "chroot_relative"
SINGLE_ACCESS: "r" | "w" | "a" | "l" | "k" | "m"
MULTI_ACCESS: "ix" | "ux" | "Ux" | "px" | "Px" | "cx" | "Cx" | "pix" | "Pix" | "cix" | "Cix" | "pux" | "PUx" | "cux" | "CUx" | "x"
NETWORK_DOMAIN: "unix" | "inet" | "ax25" | "ipx" | "appletalk" | "netrom" | "bridge" | "atmpvc" | "x25" | "inet6" | "rose" | "netbeui" | "security" | "key" | "netlink" | "packet" | "ash" | "econet" | "atmsvc" | "rds" | "sna" | "irda" | "pppox" | "wanpipe" | "llc" | "ib" | "mpls" | "can" | "tipc" | "bluetooth" | "iucv" | "rxrpc" | "isdn" | "phonet" | "ieee802154" | "caif" | "alg" | "nfc" | "vsock" | "kcm" | "qipcrtr" | "smc" | "xdp"
NETWORK_TYPE: "stream" | "dgram" | "seqpacket" | "rdm" | "raw" | "packet" | "tcp" | "udp" | "icmp"

start: (preamble_element | profile)*

preamble_element: variable_assignment | alias_rule | include_rule | abi_rule

variable_assignment: VARIABLE ("=" | "+=") value_list
value_list: (QUOTED_STRING | UNQUOTED_VALUE)+

alias_rule: "alias" QUOTED_STRING "->" QUOTED_STRING ","

INCLUDE_DIRECTIVE: "#include"
include_rule: INCLUDE_DIRECTIVE ["if exists"] (QUOTED_STRING | MAGIC_PATH)

abi_rule: "abi" (QUOTED_STRING | UNQUOTED_VALUE) [","]

profile: ["profile"] profile_name [attachment] [flags] "{" rules "}"

profile_name: QUOTED_STRING | UNQUOTED_FILEGLOB | UNQUOTED_PROFILE_NAME
attachment: UNQUOTED_FILEGLOB
flags: ["flags="] "(" flag_list ")"
flag_list: PROFILE_FLAG ("," PROFILE_FLAG)*

rules: (line_rule | comma_rule [","] | block_rule)*

line_rule: (COMMENT | include_rule | abi_rule)

comma_rule: file_rule | link_rule | capability_rule | network_rule

capability_rule: qualifiers "capability" capability_list
capability_list: CAPABILITY+

network_rule: qualifiers "network" [NETWORK_DOMAIN] [NETWORK_TYPE]

file_rule: qualifiers ["owner"] file_rule_body
file_rule_body: ("file" fileglob access | "file" access fileglob | fileglob access | access fileglob) ["->" exec_target]
fileglob: ["/"] (UNQUOTED_FILEGLOB | VARIABLE) ( ["/"] UNQUOTED_FILEGLOB | VARIABLE)*
access: (SINGLE_ACCESS | MULTI_ACCESS)+ //-> exclude_w_a_combination
exec_target: profile_name

link_rule: qualifiers ["owner"] "link" ["subset"] fileglob "->" fileglob

block_rule: subprofile
subprofile: "profile" profile_name [attachment] [flags] "{" rules "}"

qualifiers: ["audit"] ["allow" | "deny"]

change_profile_rule: "change_profile" [exec_mode] [fileglob] ["->" profile_target]
exec_mode: "safe" | "unsafe"
profile_target: profile_name | "{" profile_name ("," profile_name)* "}"